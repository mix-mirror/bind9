/*
 * Copyright (C) Internet Systems Consortium, Inc. ("ISC")
 *
 * SPDX-License-Identifier: MPL-2.0
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, you can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * See the COPYRIGHT file distributed with this work for additional
 * information regarding copyright ownership.
 */

/* Sample SystemTap script for tracing addrdataset DNS DB opertaion in qpcache */

global qp_timer

global addrdataset_normal
global addrdataset_overmem

probe
process("libdns.so").mark("qpcache_addrdataset_start")
{
    qp_timer[$arg1, $arg2] = ktime_get_ns()
}

probe
process("libdns.so").mark("qpcache_addrdataset_done")
{
    duration = ktime_get_ns() - qp_timer[$arg1, $arg2]
    delete qp_timer[$arg1, $arg2]

    if ($arg3 != 0) {
        addrdataset_overmem[pid(), $arg1]  <<< duration
    } else {
        addrdataset_normal[pid(), $arg1]  <<< duration
    }
}

probe
timer.s(5),
end
{
    foreach ([p, a] in addrdataset_normal) {
        printf(">>>> [NORMAL] pid: %ld, qpcache: 0x%xl\n", p, a)
        print(@hist_log(addrdataset_normal[p, a]))
    }

    foreach ([p, a] in addrdataset_overmem) {
        printf(">>>> [OVERMEM] pid: %ld, qpcache: 0x%xl\n", p, a)
        print(@hist_log(addrdataset_overmem[p, a]))
    }

    printf("====\n\n");

    delete addrdataset_normal
    delete addrdataset_overmem
}
